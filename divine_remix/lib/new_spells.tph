/////                                                  \\\\\
///// changes to existing spells                       \\\\\
/////                                                  \\\\\
   
ACTION_IF ((FILE_EXISTS ~divine_remix/languages/%LANGUAGE%/utf8_kit_sphere.tra~) OR (FILE_EXISTS ~divine_remix/languages/%LANGUAGE%/kit_sphere.tra~)) THEN BEGIN

  OUTER_SPRINT macro_lang "%LANGUAGE%"

END ELSE BEGIN

  OUTER_SPRINT macro_lang "english"

END

ACTION_IF FILE_EXISTS ~divine_remix/languages/%macro_lang%/utf8_kit_sphere.tra~ THEN BEGIN

  LOAD_TRA ~divine_remix/languages/%macro_lang%/utf8_kit_sphere.tra~

END ELSE BEGIN

  LOAD_TRA ~divine_remix/languages/%macro_lang%/kit_sphere.tra~

END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~eff_p32.wav~ THEN BEGIN

  COPY ~divine_remix/wav/eff_p32.wav~ ~override~

END

COPY_EXISTING ~sppr317.spl~ ~override~ // cure disease - no longer cures blindness or deafness (DR change)
  SAY UNIDENTIFIED_DESC @125
  LPF DELETE_EFFECT INT_VAR match_opcode = 75 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 81 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 8 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 112 END
  BUT_ONLY

OUTER_SPRINT level_match  @2027 // needed to change descripts, below

// move protection from evil 10' to level 3
COPY_EXISTING ~sppr408.spl~ ~override~
ADD_SPELL ~override/sppr408.spl~ 1 3 CLERIC_PROTECTION_FROM_EVIL_10_FOOT
  WRITE_LONG 0x34 3
  LPF ALTER_EFFECT INT_VAR match_power = 4 power = 3 END
  PATCH_FOR_EACH offset IN 0x50 BEGIN
    READ_LONG ~%offset%~ desc_strref
    PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
      READ_STRREF ~%offset%~ desc
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~%level_match%~ ~\1 3~ // change level in description to 3
      END
    END
  END

STRING_SET_EVALUATE desc_strref "%desc%" // change default descript

// move negative plane protection to level 3
COPY_EXISTING ~sppr413.spl~ ~override~
ADD_SPELL ~override/sppr413.spl~ 1 3 CLERIC_NEGATIVE_PLANE_PROTECTION
  WRITE_LONG 0x34 3
  LPF ALTER_EFFECT INT_VAR match_power = 4 power = 3 END
  PATCH_FOR_EACH offset IN 0x50 BEGIN
    READ_LONG ~%offset%~ desc_strref
    PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
      READ_STRREF ~%offset%~ desc
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~%level_match%~ ~\1 3~ // change level in description to 3
      END
    END
  END

STRING_SET_EVALUATE desc_strref "%desc%" // change default descript

COPY_EXISTING ~sppr508.spl~ ~override~
  READ_LONG 0x08 name

COPY ~divine_remix/spl/sppr508.spl~ ~override~ // chaotic commands - major changes
  WRITE_LONG 0x08 name
  SAY UNIDENTIFIED_DESC @168

/////                                                  \\\\\
///// new spells                                       \\\\\
/////                                                  \\\\\
  
ACTION_IF ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CAUSE_MEDIUM_WOUNDS~)) < 0) BEGIN // because this is a mainline iwdee spell, no overwrite option

  ADD_SPELL ~divine_remix/spl/cdcausmw.spl~ 1 1 CLERIC_CAUSE_MEDIUM_WOUNDS // cause medium wounds
    SAY NAME1 @204
    SAY NAME2 @204
    SAY UNIDENTIFIED_DESC @205
    SAY DESC @205
  
  COPY ~divine_remix/itm/cdcausmw.itm~ ~override~
    SAY NAME1 @204
    SAY NAME2 @204
    SAY UNIDENTIFIED_DESC @205
    SAY DESC @205

  COPY ~divine_remix/bam/cdcausma.bam~ ~override~
       ~divine_remix/bam/cdcausmb.bam~ ~override~
       ~divine_remix/bam/cdcausmc.bam~ ~override~
       ~divine_remix/bam/sppr414a.bam~ ~override~
       ~divine_remix/bam/sppr414b.bam~ ~override~
       ~divine_remix/bam/sppr414c.bam~ ~override~
       ~divine_remix/bam/sppr510a.bam~ ~override~
       ~divine_remix/bam/sppr510b.bam~ ~override~
       ~divine_remix/bam/sppr510c.bam~ ~override~

  COPY_EXISTING ~critical.itm~ ~override~ // adjusts BAMs for existing 'Cause X Wounds' items
    WRITE_ASCII 0x3A ~sppr510b~
    READ_SHORT 0x68 "abil_num"
    READ_LONG  0x6A "abil_off"
    FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
      WRITE_ASCII (0x04 + "%abil_off%" + ("%index%" * 0x38)) ~sppr510b~
    END
  
  COPY_EXISTING ~serious.itm~  ~override~ // adjusts BAMs for existing 'Cause X Wounds' items
    WRITE_ASCII 0x3A ~sppr414b~
    READ_SHORT 0x68 "abil_num"
    READ_LONG  0x6A "abil_off"
    FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
      WRITE_ASCII (0x04 + "%abil_off%" + ("%index%" * 0x38)) ~sppr414b~
    END
  
  COPY_EXISTING ~harm.itm~     ~override~ // adjusts BAMs for existing 'Cause X Wounds' items
    WRITE_ASCII 0x3A ~sppr608b~
    READ_SHORT 0x68 "abil_num"
    READ_LONG  0x6A "abil_off"
    FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
      WRITE_ASCII (0x04 + "%abil_off%" + ("%index%" * 0x38)) ~sppr608b~
    END

END

ACTION_IF ((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CAUSE_BLINDNESS_OR_DEAFNESS~)) < 0)) BEGIN  // only if not present or overwrite selected

  ADD_SPELL ~divine_remix/spl/nmcausbd.spl~ 1 3 CLERIC_CAUSE_BLINDNESS_OR_DEAFNESS // cause blindness or deafness
    SAY NAME1 @181
    SAY NAME2 @181
    SAY UNIDENTIFIED_DESC @182
    SAY DESC @182
  
  COPY ~divine_remix/bam/nmcausba.bam~ ~override~
       ~divine_remix/bam/nmcausbb.bam~ ~override~
       ~divine_remix/bam/nmcausbc.bam~ ~override~

END

ACTION_IF (((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CURE_BLINDNESS_OR_DEAFNESS~)) < 0)) AND ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CURE_BLIND_DEAF~)< 0))) BEGIN // only if not present or overwrite selected

  ADD_SPELL ~divine_remix/spl/nmcurebd.spl~ 1 3 CLERIC_CURE_BLINDNESS_OR_DEAFNESS  // cure blindness or deafness
    SAY NAME1 @179
    SAY NAME2 @179
    SAY UNIDENTIFIED_DESC @180
    SAY DESC @180

  COPY ~divine_remix/bam/nmcureba.bam~ ~override~
       ~divine_remix/bam/nmcurebb.bam~ ~override~
       ~divine_remix/bam/nmcurebc.bam~ ~override~

END

ACTION_IF ((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_DETECT_GOOD~)) < 0)) BEGIN // only if not present or overwrite selected

  ADD_SPELL ~divine_remix/spl/cddtgood.spl~ 1 1 CLERIC_DETECT_GOOD  // detect good
    SAY NAME1 @194
    SAY NAME2 @194
    SAY UNIDENTIFIED_DESC @195
    SAY DESC @195

  COPY ~divine_remix/bam/cddtgooa.bam~ ~override~
       ~divine_remix/bam/cddtgoob.bam~ ~override~
       ~divine_remix/bam/cddtgooc.bam~ ~override~

END

ACTION_IF ((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_DIVINE_SHELL~)) < 0)) BEGIN // only if not present or overwrite selected

  ADD_SPELL ~divine_remix/spl/cddshel.spl~ 1 6 CLERIC_DIVINE_SHELL  // divine shell
    SAY NAME1 @166
    SAY NAME2 @166
    SAY UNIDENTIFIED_DESC @167
    SAY DESC @167

  COPY ~divine_remix/bam/cddshela.bam~ ~override~
       ~divine_remix/bam/cddshelb.bam~ ~override~
       ~divine_remix/bam/cddshelc.bam~ ~override~

END

ACTION_IF ((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_FAERIE_FIRE~)) < 0)) BEGIN // only if not present or overwrite selected

  ADD_SPELL ~divine_remix/spl/cdfaerie.spl~ 1 1 CLERIC_FAERIE_FIRE  // faerie fire
    SAY NAME1 @112
    SAY NAME2 @112
    SAY UNIDENTIFIED_DESC @113
    SAY DESC @113

  COPY ~divine_remix/bam/cdfaeria.bam~ ~override~
       ~divine_remix/bam/cdfaerib.bam~ ~override~
       ~divine_remix/bam/cdfaeric.bam~ ~override~

END

ACTION_IF ((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_IMPREGNABLE_MIND~)) < 0)) BEGIN // only if not present or overwrite selected

  ADD_SPELL ~divine_remix/spl/nmimpreg.spl~ 1 5 CLERIC_IMPREGNABLE_MIND
    SAY NAME1 @171
    SAY NAME2 @171
    SAY UNIDENTIFIED_DESC @172
    SAY DESC @172

  COPY ~divine_remix/bam/nmimprea.bam~ ~override~
       ~divine_remix/bam/nmimpreb.bam~ ~override~
       ~divine_remix/bam/nmimprec.bam~ ~override~

END

ACTION_IF ((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_PROTECT_FROM_GOOD~)) < 0)) BEGIN // only if not present or overwrite selected

  COPY_EXISTING ~sppr107.spl~ ~override~

  ADD_SPELL ~override/sppr107.spl~ 1 1 CLERIC_PROTECT_FROM_GOOD
    SAY NAME1 @196
    SAY NAME2 @196
    SAY UNIDENTIFIED_DESC @197
    SAY DESC @197
    WRITE_ASCII 0x3a ~cdprgooc~
    READ_LONG   0x64 "abil_off"
    READ_SHORT  0x68 "abil_num"
    READ_LONG   0x6a "fx_off"
    READ_SHORT  0x70 "fx_num"
    READ_SHORT ("%abil_off%" + 0x1e + (("%abil_num%" - 1) * 0x28)) "abil_fx_num"
    READ_SHORT ("%abil_off%" + 0x20 + (("%abil_num%" - 1) * 0x28)) "abil_fx_idx"
    FOR (index = 0 ; index < (abil_fx_num + abil_fx_idx) ; index = index + 1) BEGIN
      READ_SHORT ("%fx_off%" + (0x30 * "%index%")) "opcode"
      PATCH_IF ("%opcode%" = 139) BEGIN
        SAY ("%fx_off%" + 0x04 + (0x30 * "%index%")) @201
      END ELSE
      PATCH_IF ("%opcode%" = 219) BEGIN
        WRITE_LONG ("%fx_off%" + 0x04 + (0x30 * "%index%")) 1
      END ELSE
      PATCH_IF ("%opcode%" = 233) BEGIN
        WRITE_LONG ("%fx_off%" + 0x04 + (0x30 * "%index%")) 2 // for detectable spells
      END ELSE
      PATCH_IF ("%opcode%" = 100) BEGIN
        WRITE_LONG ("%fx_off%" + 0x04 + (0x30 * "%index%")) 156
        WRITE_LONG ("%fx_off%" + 0x08 + (0x30 * "%index%")) 4
      END
    END
    FOR (index2 = 0 ; index2 < abil_num ; index2 = index2 + 1) BEGIN
      WRITE_ASCII ("%abil_off%" + 0x04 + ("%index2%" * 0x28)) ~cdprgoob~
    END

  COPY ~divine_remix/bam/cdprgooa.bam~ ~override~
       ~divine_remix/bam/cdprgoob.bam~ ~override~
       ~divine_remix/bam/cdprgooc.bam~ ~override~

  COPY_EXISTING ~statdesc.2da~ ~override~ // changes Protected from Evil icon description to Protection from Alignment
    REPLACE_TEXTUALLY ~^9[ %TAB%].+$~ ~9 CDREPLACE~
    REPLACE ~CDREPLACE~ @206
    PRETTY_PRINT_2DA

END

ACTION_IF ((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_PROTECTION_FROM_GOOD_10_FOOT~)) < 0)) BEGIN // only if not present or overwrite selected

  COPY_EXISTING ~sppr408.spl~ ~override~

  ADD_SPELL ~override/sppr408.spl~ 1 3 CLERIC_PROTECTION_FROM_GOOD_10_FOOT
    SAY NAME1 @198
    SAY NAME2 @198
    SAY UNIDENTIFIED_DESC @199
    SAY DESC @199
    WRITE_ASCII 0x3A ~cdprgd1c~
    WRITE_LONG  0x34 3
    READ_LONG   0x64 "abil_off"
    READ_SHORT  0x68 "abil_num"
    READ_LONG   0x6a "fx_off"
    READ_SHORT  0x70 "fx_num"
    READ_SHORT ("%abil_off%" + 0x1e + (("%abil_num%" - 1) * 0x28)) "abil_fx_num"
    READ_SHORT ("%abil_off%" + 0x20 + (("%abil_num%" - 1) * 0x28)) "abil_fx_idx"
    FOR (index = 0 ; index < (abil_fx_num + abil_fx_idx) ; index = index + 1) BEGIN
      READ_SHORT ("%fx_off%" + (0x30 * "%index%")) "opcode"
      PATCH_IF ("%opcode%" = 139) BEGIN
        SAY ("%fx_off%" + 0x04 + (0x30 * "%index%")) @201
      END ELSE
      PATCH_IF ("%opcode%" = 219) BEGIN
        WRITE_LONG ("%fx_off%" + 0x04 + (0x30 * "%index%")) 1
      END ELSE
      PATCH_IF ("%opcode%" = 233) BEGIN
        WRITE_LONG ("%fx_off%" + 0x04 + (0x30 * "%index%")) 2 // for detectable spells
      END ELSE
      PATCH_IF ("%opcode%" = 100) BEGIN
        WRITE_LONG ("%fx_off%" + 0x04 + (0x30 * "%index%")) 156
        WRITE_LONG ("%fx_off%" + 0x08 + (0x30 * "%index%")) 4
      END
    END
    FOR (index2 = 0 ; index2 < abil_num ; index2 = index2 + 1) BEGIN
      WRITE_ASCII ("%abil_off%" + 0x04 + ("%index2%" * 0x28)) ~cdprgd1b~
    END

  COPY ~divine_remix/bam/cdprgd1a.bam~ ~override~
       ~divine_remix/bam/cdprgd1b.bam~ ~override~
       ~divine_remix/bam/cdprgd1c.bam~ ~override~

  COPY_EXISTING ~statdesc.2da~ ~override~ // changes Protected from Evil icon description to Protection from Alignment
    REPLACE_TEXTUALLY ~^9[ %TAB%].+$~ ~9 CDREPLACE~
    REPLACE ~CDREPLACE~ @206
    PRETTY_PRINT_2DA

END

ACTION_IF (((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_RESIST_ACID_AND_CORROSION~)) < 0)) AND ((IDS_OF_SYMBOL (~spell~ ~CLERIC_RESIST_ACID_CORROSION~)) < 0)) BEGIN // only if not present or overwrite selected

  ADD_SPELL ~divine_remix/spl/nmresacd.spl~ 1 2 CLERIC_RESIST_ACID_AND_CORROSION
    SAY NAME1 @173
    SAY NAME2 @173
    SAY UNIDENTIFIED_DESC @174
    SAY DESC @174

  COPY ~divine_remix/bam/nmresaca.bam~ ~override~
       ~divine_remix/bam/nmresacb.bam~ ~override~
       ~divine_remix/bam/nmresacc.bam~ ~override~

END

ACTION_IF ((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_STRENGTH_OF_STONE~)) < 0)) BEGIN // only if not present or overwrite selected

  ADD_SPELL ~divine_remix/spl/nmstrstn.spl~ 1 1 CLERIC_STRENGTH_OF_STONE
    SAY NAME1 @175
    SAY NAME2 @175
    SAY UNIDENTIFIED_DESC @176
    SAY DESC @176

  COPY ~divine_remix/bam/nmstrsta.bam~ ~override~
       ~divine_remix/bam/nmstrstb.bam~ ~override~
       ~divine_remix/bam/nmstrstc.bam~ ~override~

END

ACTION_IF ((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CONJURE_AIR_ELEMENTAL~)) < 0)) BEGIN // only if not present or overwrite selected

  COPY_EXISTING ~spwi622.spl~ ~override~

  ADD_SPELL ~override/spwi622.spl~ 1 7 CLERIC_CONJURE_AIR_ELEMENTAL // Don't ask me why... but it works not with air elemental copy, add to be earth elemental
    WRITE_SHORT 0x1c 2
    WRITE_SHORT 0x1e 0
    WRITE_SHORT 0x34 7
    LPF DELETE_EFFECT INT_VAR match_opcode = 165 END // removing the mental combat visual effect for priest
	LPF DELETE_EFFECT INT_VAR match_opcode = 215 END // removing the mental combat visual effect for priest
	LPF DELETE_EFFECT INT_VAR match_opcode = 142 END // removing the mental combat visual effect for priest
	REPLACE_TEXTUALLY SPEART1 DRAIR1 (8)
    REPLACE_TEXTUALLY SPEART2 DRAIR2 (8)
    REPLACE_TEXTUALLY SPEART3 DRAIR3 (8)
	REPLACE_TEXTUALLY CASE_INSENSITIVE SPWI622A SPWI621A (8)
    REPLACE_TEXTUALLY CASE_INSENSITIVE SPWI622B SPWI621B (8)
    REPLACE_TEXTUALLY CASE_INSENSITIVE SPWI622C SPWI621C (8)
	WRITE_LONG 0x8 24839
	WRITE_LONG 0x50 24840
	LPF ALTER_EFFECT INT_VAR match_power = 6 power = 7 END
    PATCH_FOR_EACH offset IN 0x50 BEGIN
      READ_LONG ~%offset%~ desc_strref
      PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
        READ_STRREF ~%offset%~ desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
          REPLACE_TEXTUALLY ~%level_match%~ ~\1 7~ // change level in description to 7
        END
      END
      SAY_EVALUATED offset ~%desc%~
    END

// Cretaing the new creatuers to remove the mental combat script for priest
	COPY_EXISTING "SPAIR1.EFF" "override/DRAIR1.EFF" WRITE_ASCIIE 0x30 DRAIRPR2 (8)
    COPY_EXISTING "SPAIR2.EFF" "override/DRAIR2.EFF" WRITE_ASCIIE 0x30 DRAIRPR3 (8)
    COPY_EXISTING "SPAIR3.EFF" "override/DRAIR3.EFF" WRITE_ASCIIE 0x30 DRAIRPR4 (8)
    
	COPY_EXISTING "ELAIRSU2.CRE" "override/DRAIRPR2.cre"
	  WRITE_ASCII 0x248 "None" #8
	COPY_EXISTING "ELAIRSU3.CRE" "override/DRAIRPR3.cre"
	  WRITE_ASCII 0x248 "None" #8
	COPY_EXISTING "ELAIRSU4.CRE" "override/DRAIRPR4.cre"
	  WRITE_ASCII 0x248 "None" #8

END

ACTION_IF ((overwrite = 1) OR ((IDS_OF_SYMBOL (~spell~ ~CLERIC_ENERGY_DRAIN~)) < 0)) BEGIN // only if not present or overwrite selected

  COPY_EXISTING ~spwi914.spl~ ~override~

  ADD_SPELL ~override/spwi914.spl~ 1 7 CLERIC_ENERGY_DRAIN
    WRITE_SHORT 0x1c 2
    WRITE_SHORT 0x1e 0
    WRITE_SHORT 0x34 7
    LPF ALTER_EFFECT INT_VAR match_power = 9 power = 7 END
    PATCH_FOR_EACH offset IN 0x50 BEGIN
      READ_LONG ~%offset%~ desc_strref
      PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
        READ_STRREF ~%offset%~ desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
          REPLACE_TEXTUALLY ~%level_match%~ ~\1 7~ // change level in description to 7
        END
      END
      SAY_EVALUATED offset ~%desc%~
    END

END

// disable original spells that were moved to another level
COPY_EXISTING ~sppr408.spl~ ~override~
              ~sppr413.spl~ ~override~
  WRITE_BYTE 0x21 (THIS BOR (BIT6 + BIT7)) // don't show up automatically in spellbooks
  BUT_ONLY